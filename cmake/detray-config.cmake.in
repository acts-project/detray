# Detray library, part of the ACTS project (R&D line)
#
# (c) 2021 CERN for the benefit of the ACTS project
#
# Mozilla Public License Version 2.0

# Set up the helper functions/macros.
@PACKAGE_INIT@

# Set up variables describing which components were enabled during the
# Detray build.
set( DETRAY_VECMEM_PLUGIN @DETRAY_VECMEM_PLUGIN@ )
set( DETRAY_EIGEN_PLUGIN @DETRAY_EIGEN_PLUGIN@ )
set( DETRAY_SMATRIX_PLUGIN @DETRAY_SMATRIX_PLUGIN@ )
set( DETRAY_VC_PLUGIN @DETRAY_VC_PLUGIN@ )
set( DETRAY_IO_CSV @DETRAY_IO_CSV@ )
set( DETRAY_DISPLAY @DETRAY_DISPLAY@ )
set( DETRAY_BUILD_CUDA @DETRAY_BUILD_CUDA@ )

# Find all packages that Detray needs to function.
include( CMakeFindDependencyMacro )
find_dependency( algebra-plugins )
if( DETRAY_VECMEM_PLUGIN )
   find_dependency( vecmem )
endif()

# Set up some simple variables for using the package.
set( detray_VERSION "@PROJECT_VERSION@" )
set_and_check( detray_INCLUDE_DIR
   "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@" )
set_and_check( detray_CMAKE_DIR "@PACKAGE_CMAKE_INSTALL_CMAKEDIR@" )

# Print a standard information message about the package being found.
include( FindPackageHandleStandardArgs )
find_package_handle_standard_args( algebra-plugins REQUIRED_VARS
   CMAKE_CURRENT_LIST_FILE
   VERSION_VAR detray_VERSION )

# Include the file listing all the imported targets and options.
include( "${detray_CMAKE_DIR}/detray-config-targets.cmake" )
